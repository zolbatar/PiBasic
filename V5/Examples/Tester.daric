30 T% = Time
40 PRINT "DARIC Test Rig"
50 PRINT "--------------"
60 PRINT
70 :
100 PRINT "::Arrays::"
110 DIM A%(6)
120 A%(0) = 10
130 A%(5) = 15
140 PRINT "Index 0            = ";:IF A%(0) = 10 THEN PROCPass() ELSE PROCFail()
150 PRINT "Index 5            = ";:IF A%(5) = 15 THEN PROCPass() ELSE PROCFail()
160 DIM ta1$(2)
170 ta1$(0) = "Test"
180 ta1$(1) = ta1$(0)
190 PRINT "String arrays      = ";:IF ta1$(1) = "Test" THEN PROCPass() ELSE PROCFail()
200 DIM ta2$(2, 3)
210 ta2$(0, 1) = "Test"
220 ta2$(1, 2) = ta2$(0, 1)
230 PRINT "2D arrays          = ";:IF ta2$(1, 2) = "Test" THEN PROCPass() ELSE PROCFail()
240 PRINT
250 :
500 REM PRINT "::BBC Basic Compatibility::"
510 REM PRINT "LET                = ";:LETA=1:IF A = 1 THEN PROCPass() ELSE PROCFail()
520 PRINT
530 :
600 PRINT "::Boolean::"
610 PRINT "Or                 = ";:IF (&F0 OR &F0 = &F0) AND (&F0 OR &0F = &FF) THEN PROCPass() ELSE PROCFail()
620 PRINT "Eor                = ";:IF (1 EOR 1 = 0) AND (1 EOR 0 = 1) AND (0 EOR 0 = 0) THEN PROCPass() ELSE PROCFail()
630 PRINT "And                = ";:IF ((&F0 AND &F0) = &F0) AND ((&F0 AND &0F) = &0) THEN PROCPass() ELSE PROCFail()
640 PRINT "Not                = ";:IF NOT 1 = 0 THEN PROCPass() ELSE PROCFail()
650 PRINT "True               = ";:IF TRUE = 1 THEN PROCPass() ELSE PROCFail()
660 PRINT "False              = ";:IF FALSE = 0 THEN PROCPass() ELSE PROCFail()
670 PRINT
680 :
700 PRINT "::Comparison::"
710 PRINT "Int =              = ";:IF 5 = 5 THEN PROCPass() ELSE PROCFail()
720 PRINT "Int <>             = ";:IF 5 <> 6 THEN PROCPass() ELSE PROCFail()
730 PRINT "Int <              = ";:IF 5 < 10 THEN PROCPass() ELSE PROCFail()
740 PRINT "Int <= (<)         = ";:IF 4 <= 5 THEN PROCPass() ELSE PROCFail()
750 PRINT "Int <= (=)         = ";:IF 5 <= 5 THEN PROCPass() ELSE PROCFail()
760 PRINT "Int >              = ";:IF 10 > 5 THEN PROCPass() ELSE PROCFail()
770 PRINT "Int >= (>)         = ";:IF 10 >= 5 THEN PROCPass() ELSE PROCFail()
780 PRINT "Int >= (>=)        = ";:IF 5 >= 5 THEN PROCPass() ELSE PROCFail()
790 PRINT "Float =            = ";:IF 5.0 = 5.0 THEN PROCPass() ELSE PROCFail()
800 PRINT "Float <>           = ";:IF 5.0 <> 6.0 THEN PROCPass() ELSE PROCFail()
810 PRINT "Float <            = ";:IF 5.0 < 10.0 THEN PROCPass() ELSE PROCFail()
820 PRINT "Float <= (<)       = ";:IF 4.0 <= 5.0 THEN PROCPass() ELSE PROCFail()
830 PRINT "Float <= (=)       = ";:IF 5.0 <= 5.0 THEN PROCPass() ELSE PROCFail()
840 PRINT "Float >            = ";:IF 10.0 > 5.0 THEN PROCPass() ELSE PROCFail()
850 PRINT "Float >= (>)       = ";:IF 10.0 >= 5.0 THEN PROCPass() ELSE PROCFail()
860 PRINT "Float >= (>=)      = ";:IF 5.0 >= 5.0 THEN PROCPass() ELSE PROCFail()
870 PRINT "String =           = ";:IF "Fred" = "Fred" THEN PROCPass() ELSE PROCFail()
880 PRINT "String <>          = ";:IF "Fred" <> "fred" THEN PROCPass() ELSE PROCFail()
890 PRINT "String <           = ";:IF "Fred" < "fred" THEN PROCPass() ELSE PROCFail()
900 PRINT "String <= (<)      = ";:IF "Fred" <= "fred" THEN PROCPass() ELSE PROCFail()
910 PRINT "String <= (=)      = ";:IF "Fred" <= "Fred" THEN PROCPass() ELSE PROCFail()
920 PRINT "String >           = ";:IF "fred" > "Fred" THEN PROCPass() ELSE PROCFail()
930 PRINT "String >= (>)      = ";:IF "fred" >= "Fred" THEN PROCPass() ELSE PROCFail()
940 PRINT "String >= (>=)     = ";:IF "Fred" >= "Fred" THEN PROCPass() ELSE PROCFail()
950 PRINT
960 :
1200 PRINT "::Conversion::"
1210 PRINT "10 + 15.5          = ";:IF 10 + 15.5 = 25.5 THEN PROCPass() ELSE PROCFail()
1220 PRINT
1230 :
1500 PRINT "::Data::"
1510 LET d1% = 0, d2$ = "", d3 = 0.0
1520 DIM dat%(5)
1530 Read d1%, d2$, d3
1540 Data 100, "Fred", 10.5
1550 PRINT "Data (Int)         = ";:IF d1% = 100 THEN PROCPass() ELSE PROCFail()
1560 PRINT "Data (Float)       = ";:IF d3 = 10.5 THEN PROCPass() ELSE PROCFail()
1570 PRINT "Data (String)      = ";:IF d2$ = "Fred" THEN PROCPass() ELSE PROCFail()
1580 FOR I% = 0 TO 4:READ dat%(I%):NEXT
1610 DATA 1, 2, 3, 4, 5
1620 PRINT "Data (Array)       = ";:IF dat%(0) + dat%(1) + dat%(2) + dat%(3) + dat%(4) = 15 THEN PROCPass() ELSE PROCFail()
1630 DATA 1, 2, 3, 4, 5:REM used by function FuncTestLocalVariables. Data can't be inside functions yet
1640 PROCTestLocalVariables()
1650 RESTORE
1660 READ d1%
1670 PRINT "ResTOre            = ";:IF d1% = 100 THEN PROCPass() ELSE PROCFail()
1680 PRINT
1690 :
1800 PRINT "::File I/O::"
1810 PRINT "Write THEN Read    = ";
1820 C%=OPENOUT("Test")
1830 BPUT# C%, ASC("A")
1840 CLOSE# C%
1850 C%=OPENIN("Test")
1860 V%=BGET# C%
1870 IF V% = 65 THEN PROCPass() ELSE PROCFail()
1880 CLOSE# C%
1890 PRINT
1900 :
2000 PRINT "::Functions::"
2010 PRINT "Function (Return)  = ";:PROCTest()
2020 PRINT "Function (Int)     = ";:IF FNTestReturn%(10, 5.5) = 20 THEN PROCPass() ELSE PROCFail()
2030 PRINT "Function (Float)   = ";:IF FNTestReturn(10, 5.5) = 20.5 THEN PROCPass() ELSE PROCFail()
2040 PRINT "Proc (RETURN)      = ";:AA%=1:BB%=2:PROCTestRet(AA%, BB%):IF AA% = 100 And BB%=200 THEN PROCPass() ELSE PROCFail()
2050 PRINT	
2060 :
2070 GOSUB 7000:REM GOSUB & GOTO
2080 :
2100 PRINT "::Locals & Globals::"
2110 G%=5
2120 PROCLocalVars()
2130 PRINT "Preserving Global  = ";:IF G% = 5 THEN PROCPass() ELSE PROCFail()
2140 PRINT
2150 :
2200 PRINT "::Looping::"	
2210 PRINT "One line IF        = ";:IF 1 = 1 THEN PROCPass() ELSE PROCFail()
2220 PRINT "Multiline IF       = ";
2230 IF 1 = 1
2240 	PROCPass()
2250 ELSE
2260 	PROCFail()
2270 ENDIF
2280 I% = 0
2290 Repeat
2300 	I% = I% + 1
2310 Until I% = 10
2320 PRINT "Repeat             = ";:IF I% = 10 THEN PROCPass() ELSE PROCFail()
2330 I% = 0
2340 While I% < 10
2350 	I% = I% + 1
2360 EndWhile
2370 PRINT "While              = ";:IF I% = 10 THEN PROCPass() ELSE PROCFail()
2380 I% = 0
2390 FOR J% = 0 TO 9
2400 	I% = I% + 1
2410 NEXT
2420 PRINT "FOR (Int)          = ";:IF I% = 10 THEN PROCPass() ELSE PROCFail()
2430 I% = 0
2400 FOR J% = 0 TO 9 Step 2
2450 	I% = I% + 1
2460 NEXT
2470 PRINT "FOR + Step (Int)   = ";:IF I% = 5 THEN PROCPass() ELSE PROCFail()
2480 I% = 0
2490 FOR J = 0 TO 9
2500 	I% = I% + 1
2510 NEXT
2520 PRINT "FOR (Float)        = ";:IF I% = 10 THEN PROCPass() ELSE PROCFail()
2530 I% = 0
2540 FOR J = 0 TO 9 Step 2
2550 	I% = I% + 1
2560 NEXT
2570 PRINT "FOR + Step (Float) = ";:IF I% = 5 THEN PROCPass() ELSE PROCFail()
2580 PRINT "Case               = ";
2590 I%=1
2600 CASE I% OF
2610 	WHEN 0, 1, 2: PROCPass()
2620 	WHEN 3: PROCFail()
2620 	OTHERWISE PRINT "Neither"
2630 ENDCASE
2640 PRINT "Case (Otherwise)   = ";
2650 I%=4
2660 Case I% OF
2670 	WHEN 0, 1, 2: PROCFail()
2680 	WHEN 3: PROCFail()
2690 	OTHERWISE PROCPass()
2700 ENDCASE
2710 PRINT
2729 :
3000 PRINT "::Maths::"
3010 PRINT "- (Unary)          = ";:IF 2 - -2 = 4 THEN PROCPass() ELSE PROCFail()
3020 PRINT "- (Int)            = ";:IF 10 - 8 = 2 THEN PROCPass() ELSE PROCFail()
3030 PRINT "+ (Int)            = ";:IF 10 + 8 = 18 THEN PROCPass() ELSE PROCFail()
3040 PRINT "* (Int)            = ";:IF 10 * 8 = 80 THEN PROCPass() ELSE PROCFail()
3050 PRINT "/ (Int)            = ";:IF 17 / 5 = 3.4 THEN PROCPass() ELSE PROCFail()
3060 PRINT "// (Int)           = ";:IF 17 // 5 = 3 THEN PROCPass() ELSE PROCFail()
3070 PRINT "Mod (Int)          = ";:IF 11 Mod 5 = 1 THEN PROCPass() ELSE PROCFail()
3080 PRINT "Div (Int)          = ";:IF 11 Div 5 = 2 THEN PROCPass() ELSE PROCFail()
3090 PRINT "Abs (Int)          = ";:IF Abs(-10) = 10 THEN PROCPass() ELSE PROCFail()
3100 PRINT "- (Float)          = ";:IF 10.5 - 8.25 = 2.25 THEN PROCPass() ELSE PROCFail()
3110 PRINT "+ (Float)          = ";:IF 10.4 + 8.1 = 18.5 THEN PROCPass() ELSE PROCFail()
3120 PRINT "* (Float)          = ";:IF 2.5 * 4.5 = 11.25 THEN PROCPass() ELSE PROCFail()
3130 PRINT "/ (Float)          = ";:IF 8.4 / 4.2 = 2.0 THEN PROCPass() ELSE PROCFail()
3140 PRINT "Mod (Float)        = ";:IF 11.0 Mod 5.0 = 1 THEN PROCPass() ELSE PROCFail()
3150 PRINT "Div (Float)        = ";:IF 11.0 Div 5.0 = 2 THEN PROCPass() ELSE PROCFail()
3160 PRINT "Abs (Float)        = ";:IF Abs(-10.5) = 10.5 THEN PROCPass() ELSE PROCFail()
3170 PRINT "Sqr                = ";:IF Sqr(16) = 4 THEN PROCPass() ELSE PROCFail()
3180 PRINT "Ln                 = ";:IF Int(Ln(16) * 100.0) = 277 THEN PROCPass() ELSE PROCFail()
3190 PRINT "Log                = ";:IF Int(Log(16) * 100.0) = 120 THEN PROCPass() ELSE PROCFail()
3200 PRINT "Exp                = ";:IF Int(Exp(1) * 100.0) = 271 THEN PROCPass() ELSE PROCFail()
3210 PRINT "Atn                = ";:IF Int(Atn(3) * 100.0) = 124 THEN PROCPass() ELSE PROCFail()
3220 PRINT "Tan                = ";:IF Int(Tan(3) * 100.0) = -15 THEN PROCPass() ELSE PROCFail()
3230 PRINT "Sin                = ";:IF Int(Sin(3) * 100.0) = 14 THEN PROCPass() ELSE PROCFail()
3240 PRINT "Cos                = ";:IF Int(Cos(3) * 100.0) = -99 THEN PROCPass() ELSE PROCFail()
3250 PRINT "Acs                = ";:IF Int(Acs(0.5) * 100.0) = 104 THEN PROCPass() ELSE PROCFail()
3260 PRINT "Asn                = ";:IF Int(Asn(0.5) * 100.0) = 52 THEN PROCPass() ELSE PROCFail()
3270 PRINT "Rad                = ";:IF Int(Rad(90) * 100.0) = 157 THEN PROCPass() ELSE PROCFail()
3280 PRINT "Deg                = ";:IF Int(Deg(1.5)) = 85 THEN PROCPass() ELSE PROCFail()
3290 PRINT "Sgn (-)            = ";:IF Sgn(-10) = -1 THEN PROCPass() ELSE PROCFail()
3300 PRINT "Sgn                = ";:IF Sgn(0) = 0 THEN PROCPass() ELSE PROCFail()
3310 PRINT "Sgn (+)            = ";:IF Sgn(10) = 1 THEN PROCPass() ELSE PROCFail()
3320 PRINT "<< (ShIFt left)    = ";:IF 60 << 2 = 240 THEN PROCPass() ELSE PROCFail()
3330 PRINT ">> (ShIFt right)   = ";:IF 200 >> 2 = 50 THEN PROCPass() ELSE PROCFail()
3340 PRINT "<<=                = ";:AA%=1:AA%<<=4:IF AA%=16 THEN PROCPass() ELSE PROCFail()
3350 PRINT
3360 :
3500 PRINT "::Numbers::"
3510 PRINT "Hex                = ";:IF &40 = 64 THEN PROCPass() ELSE PROCFail()
3520 PRINT "Binary             = ";:IF %100 = 4 THEN PROCPass() ELSE PROCFail()
3520 PRINT
3530 :
3600 PRINT "::PRINT & Input::"
3610 PRINT "Multi-PRINT ,      = ";:PRINT 10,151.2,"Test"
3620 PRINT "Multi-PRINT ;      = ";:PRINT 10;151.2;"Test"
3630 PRINT "Multi-PRINT end ;  = ";:PRINT 10;151.2;"Test";:PRINT
3640 PRINT
3650 :
3700 PRINT "::Strings::"
3710 PRINT "+                  = ";:IF "Bob" + "Jack" = "BobJack" THEN PROCPass() ELSE PROCFail()
3720 PRINT "Asc                = ";:IF (Asc("Fred") = 70) And (Asc("") = -1) THEN PROCPass() ELSE PROCFail()
3730 PRINT "Chr$               = ";:IF Chr$(65) = "A" THEN PROCPass() ELSE PROCFail()
3740 PRINT "Instr              = ";:IF Instr("Test this this string", "this") = 6 THEN PROCPass() ELSE PROCFail()
3750 PRINT "Instr (with start) = ";:IF Instr("Test this this string", "this", 8) = 11 THEN PROCPass() ELSE PROCFail()
3760 PRINT "Left$              = ";:IF Left$("A test string", 6) = "A test" THEN PROCPass() ELSE PROCFail()
3770 PRINT "Mid$               = ";:IF Mid$("A test string", 3, 4) = "test" THEN PROCPass() ELSE PROCFail()
3780 PRINT "Right$             = ";:IF Right$("A test string", 6) = "string" THEN PROCPass() ELSE PROCFail()
3790 PRINT "Len                = ";:IF Len("A test string") = 13 THEN PROCPass() ELSE PROCFail()
3800 PRINT "Val                = ";:IF Val("10.5") = 10.5 THEN PROCPass() ELSE PROCFail()
3810 PRINT "Str$               = ";:IF Str$(10.5) = "10.5" THEN PROCPass() ELSE PROCFail()
3820 PRINT "Str$ (Hex)         = ";:IF Str$~(15) = "F" THEN PROCPass() ELSE PROCFail()
3830 PRINT "String$            = ";:IF String$(4, "A") = "AAAA" THEN PROCPass() ELSE PROCFail()
3840 PRINT
3850 :
3900 PRINT "::Tokens::"
3910 PRINT "Swap               = ";:SWAPA%=1:SWAPB%=2:SWAP SWAPA%,SWAPB%:IF SWAPA%=2 AND SWAPB%=1 THEN PROCPass() ELSE PROCFail()
3920 PRINT
3930 :
4000 PRINT "::System/OS::"
4010 PRINT "Oscli              = ";:Oscli "FX0":PRINT " - NOT TESTED"
4020 PRINT "Time               = ";:IF Time > 0 THEN PROCPass() ELSE PROCFail()
4030 PRINT "Time$              = ";:IF Len(Time$) = 22 THEN PROCPass() ELSE PROCFail()
4040 PRINT 
4050 :
4100 PRINT "::Types::"
4110 PROCTestTypes()
4120 PRINT 
4130 :
4200 PRINT "::Keyboard::"
4210 PRINT "Inkey (timeout)    = ";:IF INKEY(10) = -1 THEN PROCPass() ELSE PROCFail()
4220 PRINT "Inkey (wait)       = ";:IF INKEY(1000) <> -1 THEN PROCPass() ELSE PROCFail()
4230 :
4300 PRINT
4310 PRINT "Complete in ";:PRINT ((Time-T%)/100.0);:PRINT " seconds"
4320 CHAIN "Welcome"
4330 END
4340 :
5000 REM Check FORward FN/PROC calls
5010 Type VertexTest
5020 	Field x
5030 	Field y
5040 	Field z
5050 	Field rgb%
5060 EndType
5070 :
5100 DEFPROCLocalVars()
5110 	Local G% = 10
5120 EndProc
5130 :
5200 DEFPROCTestTypes()
5210 	Local A! = VertexTest
5220 	A!x = 50.0
5230 	A!y = 100.5
5240 	A!rgb% = &FF2080
5250 	PRINT "Load field (float) = ";: IF A!y = 100.5 THEN PROCPass() ELSE PROCFail()
5260 	PRINT "Load field (int)   = ";: IF A!rgb% = &FF2080 THEN PROCPass() ELSE PROCFail()
5270 	DIM B!(5, VertexTest)
5280 	B!(4)y = 100.0
5290 	PRINT "Load field (array) = ";: IF B!(4)y = 100.0 THEN PROCPass() ELSE PROCFail()
5300 EndProc
5310 :
5400 DEFPROCTest()
5410 	PROCPass()
5420 	Return
5430 	PRINT "FAIL"
5440 EndProc
5450 :
5500 DEFPROCTestRet(RETURN a%, RETURN b%)
5510 	a%=100
5520 	b%=200
5530 EndProc
5540 : 
5600 DEFFNTestReturn%(x%, y) 
5610 	RETURN Int(x% + y + 5)
5620 ENDFN
5630 :
5700 DEFFNTestReturn(x%, y) 
5710 	RETURN x% + y + 5.0
5720 ENDFN
5730 :
5800 DEFPROCTestLocalVariables()
5810 	Local DIM C%(5)
5820 	FOR LOCAL J% = 0 TO 4
5830 		Read C%(J%)
5840 	NEXT
5850 	PRINT "Func Local (Array) = ";:IF C%(0) + C%(1) + C%(2) + C%(3) + C%(4) = 15 THEN PROCPass() ELSE PROCFail()
5860 EndProc
5870 :
6000 GOSUB%=1:RETURN
6010 :
6020 DEFPROCFail()
6020 	COLOUR RED
6030 	PRINT  "!! FAILED TEST !!"
6040 	END
6050 ENDPROC
6060 :
6070 DEFPROCPass()
6080 	COLOUR GREEN
6090 	PRINT "PASS"
6100 	COLOUR WHITE
6110 ENDPROC
6120 :
7000 PRINT "::GOTO & GOSUB::"
7010 PRINT "GOTO               = ";
7020 GOTO 7040
7030 PROCFail()
7040 PROCPass()
7050 PRINT "GOSUB              = ";
7060 GOSUB%=0
7070 GOSUB 6000
7080 IF GOSUB%=1 THEN PROCPass() ELSE PROCFail()
7090 RETURN
