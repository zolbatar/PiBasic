INSTALL "SetupGraphics"
INSTALL "Menus"
PROCTitle("Select a 3D Model to Load")
PROCLabel("1", "911 GT2 Rim", ms%+(0*ys%))
PROCLabel("2", "Male Head", ms%+(1*ys%))
PROCLabel("3", "Teapot", ms%+(2*ys%))
PROCLabel("Q", "Back to Welcome", ms%+(5*ys%))
FLIP
A$=GET$
CASE A$ OF
	WHEN "1" filename$="911-gt2-rim.obj"
	WHEN "2" filename$="MaleHead.obj"
	WHEN "3" filename$="Teapot.obj"
    WHEN "Q","q" CHAIN "Welcome",full%
    OTHERWISE CHAIN "Welcome",full%
ENDCASE

CLS
PROCTitle("Select a rendering type")
PROCLabel("1", "Solid flat shaded", ms%+(0*ys%))
PROCLabel("2", "Wireframe", ms%+(1*ys%))
PROCLabel("3", "Smooth shaded", ms%+(2*ys%))
PROCLabel("4", "Solid with edge", ms%+(3*ys%))
PROCLabel("Q", "Back to Welcome", ms%+(6*ys%))
FLIP
B$=GET$
CASE B$ OF
	WHEN "1","2","3","4" obj%=FNLoadOBJFile%(filename$,B$)
    WHEN "Q","q" CHAIN "Welcome",full%
    OTHERWISE CHAIN "Welcome",full%
ENDCASE

SHOWFPS
FOR I=0 TO 360 STEP 1
	CLS
	ROTATE obj%, 0, I, 0
    COLOUR &6060B0
	RENDER
	FLIP	
    IF INKEY(0) <> -1 
        DELETEOBJECT obj%
        CHAIN "Welcome",full%
    ENDIF
NEXT
DELETEOBJECT obj%
INKEY(1000)
CHAIN "Welcome",full%

DEFFNLoadOBJFile%(filename$, type$)
CLS
FLIP
    LOCAL number_of_vertices%=0, number_of_faces%=0

    PRINT "Loading ";filename$
    file%=OPENIN(filename$)

    REM First stage is to parse the file and work out how many vertices and faces we have
    line$=""
    REPEAT
        line$=GET$#file%
        PROCProcessOBJLine(line$, number_of_vertices%, number_of_faces%)
    UNTIL EOF#file%
    PRINT "Vertices: ";number_of_vertices%;", Faces: ";number_of_faces%
	DIM V!(number_of_vertices%, Vertex3D)
	DIM F!(number_of_faces%, Triangle3D)    

    REM Normalise size to 1
    PRINT "Normalising size to 1.0...";
    line$=""
    PTR#file%=0
    min=1000000:max=-1000000
    REPEAT
        line$=GET$#file%
        PROCNormaliseSize(line$, min, max)
    UNTIL EOF#file%
    PRINT "adjustment is ";max

    REM Now actually start to build our object, vertices first
    PRINT "Building vertices..."
    index%=0
    line$=""
    PTR#file%=0
    colour%=&404080
    IF type$="4" colour%=&202040
    REPEAT
        line$=GET$#file%
        PROCProcessOBJVertex(line$, index%, max, colour%)
    UNTIL EOF#file%

    REM Now build faces
    PRINT "Building faces..."
    index%=0
    line$=""
    PTR#file%=0
    REPEAT
        line$=GET$#file%
        PROCProcessOBJFace(line$, index%, colour%)
    UNTIL EOF#file%

    REM Create shape
    PRINT "Creating shape..."
	obj_shape%=SHAPE V!, F!
    sh%=0
    CASE type$ OF
        WHEN "2" sh%=1
        WHEN "3" sh%=2
        WHEN "4" sh%=3
    ENDCASE
    CASE A$ OF
        WHEN "1" obj%=OBJECT obj_shape%, 0, 0, -150, 0, 0, 0, 40, sh%
        WHEN "2" obj%=OBJECT obj_shape%, 0, -20, -150, 0, 0, 0, 25, sh%
        WHEN "3" obj%=OBJECT obj_shape%, 0, 20, -100, 0, 0, 0, 50, sh%
    ENDCASE
    RETURN obj%
ENDFN

DEFPROCProcessOBJLine(line$, RETURN number_of_vertices%, RETURN number_of_faces%)
    LOCAL s1$="",s2$="",s3$="",s4$=""
    IF LEN(line$) < 1 THEN RETURN
    CASE LEFT$(line$, 1) OF
        WHEN "v" number_of_vertices%=number_of_vertices%+1
        WHEN "f" PROCGet4OBJValues(line$, s1$, s2$, s3$, s4$):IF LEN(s4$)=0 THEN number_of_faces%=number_of_faces%+1 ELSE number_of_faces%=number_of_faces%+2
    ENDCASE
ENDPROC

DEFPROCGet3OBJValues(line$, RETURN s1$, RETURN s2$, RETURN s3$)
    p1%=INSTR(line$, " ")
    p2%=INSTR(line$, " ", p1% + 1)
    p3%=INSTR(line$, " ", p2% + 1)
    p4%=LEN(line$)
    s1$=MID$(line$,p1%+1,p2% - p1%-1)
    s2$=MID$(line$,p2%+1,p3% - p2%-1)
    s3$=MID$(line$,p3%+1,p4% - p3%)
ENDPROC

DEFPROCGet4OBJValues(line$, RETURN s1$, RETURN s2$, RETURN s3$, RETURN s4$)
    p1%=INSTR(line$, " ")
    p2%=INSTR(line$, " ", p1% + 1)
    p3%=INSTR(line$, " ", p2% + 1)
    p4%=INSTR(line$, " ", p3% + 1)
    p5%=LEN(line$)
    IF p4%=0 THEN p4%=p5%:p5%=0
    s1$=MID$(line$,p1%+1,p2% - p1%-1)
    s2$=MID$(line$,p2%+1,p3% - p2%-1)
    IF p5%=0 THEN 
        s3$=MID$(line$,p3%+1,p4% - p3%)
        s4$="" 
    ELSE 
        s3$=MID$(line$,p3%+1,p4% - p3%-1)
        s4$=MID$(line$,p4%+1,p5% - p4%)
    ENDIF
ENDPROC

DEFPROCNormaliseSize(line$, RETURN min, RETURN max)
    IF LEN(line$) < 1 THEN RETURN
    IF LEFT$(line$, 2) <> "v " THEN RETURN
    s1$="":s2$="":s3$=""
    PROCGet3OBJValues(line$, s1$, s2$, s3$)
    x=VAL(s1$)
    y=VAL(s2$)
    z=VAL(s3$)
   
    IF x < min THEN min = x
    IF y < min THEN min = y
    IF z < min THEN min = z
  
    IF x > max THEN max = x
    IF y > max THEN max = y
    IF z > max THEN max = z
ENDPROC

DEFPROCProcessOBJVertex(line$, RETURN index%, max, colour%)
    LOCAL s1$="",s2$="",s3$=""
    IF LEN(line$) < 1 THEN RETURN
    IF LEFT$(line$, 2) <> "v " THEN RETURN
    PROCGet3OBJValues(line$, s1$, s2$, s3$)
    x=VAL(s1$) / max
    y=VAL(s2$) / max
    z=VAL(s3$) / max
	VERTEX V!(index%), x, -y, -z, colour%
    index% = index% + 1
ENDPROC

DEFPROCProcessOBJFace(line$, RETURN index%, colour%)
    LOCAL s1$="",s2$="",s3$="",s4$=""
    IF LEN(line$) < 1 THEN RETURN
    IF LEFT$(line$, 2) <> "f " THEN RETURN
    PROCGet4OBJValues(line$, s1$, s2$, s3$, s4$)
    IF LEN(s4$)=0 THEN
        v1%=VAL(s1$)-1
        v2%=VAL(s2$)-1
        v3%=VAL(s3$)-1
	    FACE F!(index%), v1%, v2%, v3%, colour%
    ELSE
        v1%=VAL(s1$)-1
        v2%=VAL(s2$)-1
        v3%=VAL(s3$)-1
        v4%=VAL(s4$)-1
	    FACE F!(index%), v1%, v2%, v3%, colour%
        index% = index% + 1
	    FACE F!(index%), v1%, v3%, v4%, colour%

    ENDIF
    index% = index% + 1
ENDPROC
